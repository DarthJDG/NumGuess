> "Welcome to NumGuess Falcon version!\n"
>> "Enter your name: "
name = input()
if name == "": name = "Player"

>> "\nWelcome ", name, ", enter upper limit: "
limit = 10
try: limit = int(input())
if limit < 10: limit = 10
max_tries = int(log(limit) / log(2)) + 1

loop
	> "\nGuess my number between 1 and ", limit, "!\n"

	num = random(limit - 1) + 1
	tries = 0
	guess = 0

	loop
		>> "Guess: "
		guess = nil
		try: guess = int(input())

		if guess == nil
			> "That's just plain wrong."
		elif guess < 1 or guess > limit
			> "Out of range."
		else
			tries += 1
			if guess < num
				> "Too low!"
			elif guess > num
				> "Too high!"
			end
		end
	end guess == num

	tries_word = tries == 1 ? "try" : "tries"
	> "\nWell done ", name, ", you guessed my number from ", tries, " ", tries_word, "!"

	if tries == 1
		> "You're one lucky bastard!"
	elif tries < max_tries
		> "You are the master of this game!"
	elif tries == max_tries
		> "You are a machine!"
	elif tries <= max_tries * 1.1
		> "Very good result!"
	elif tries <= limit
		> "Try harder, you can do better!"
	else
		> "I find your lack of skill disturbing!"
	end

	>> "Play again [y/N]? "
end input().upper() != "Y"

> "\nOkay, bye."

